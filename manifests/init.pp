# @summary this class installs and configures 389 Directory Server.
#
# @param manage
#   Whether to manage Directory Server with Puppet or not. Valid values are 
#   true (default) and false.
# @param manage_config
#   Whether or not to manage Directory Server _configuration_ with Puppet. Valid 
#   values are true and false (default).
# @param manage_packetfilter
#   Whether to manage iptables/ip6tables rules with Puppet. Valid values are
#   true and false (default).
# @param manage_epel
#   Manage EPEL using this module. Valid values are true (default) and false.
# @param full_machine_name
#   Fully-qualified name for this host, or for the load balancer in front of
#   this host.
# @param serveridentifier
#   Identifier for the Directory Server instance. Defaults to $::hostname. Note 
#   that the identifier "can contain only alphanumeric characters and the 
#   following: #%:@_-"
# @param ldap_port
#   Directory Server (LDAP) port. Defaults to 389.
# @param suffix
#   Directory tree suffix. For example "dc=domain,dc=com". No default value.
# @param rootdn
#   Root DN (i.e. "Directory Manager") for the Directory Server. Defaults to 
#   'cn=Directory Manager'.
# @param rootdn_pwd
#   Password for the Root DN user.
# @param config_directory_ldap_url
#   LDAP URL of the Config Directory. The Config Directory will get generated by 
#   the setup-ds-admin script. The default value 
#   ('ldap://localhost:389/o=NetscapeRoot') should not be changed until this 
#   module supports adding new Directory Server instances that don't 
#   (necessarily) have their own local Config Directory server.
# @param config_directory_admin_id
#   Admin user for the Configuration Directory. Defaults to 'admin'.
# @param config_directory_admin_pwd
#   Password for the Configuration Directory admin user.
# @param allow_anonymous_access
#   Level of anonymous access to allow. Valid values 'on', 'off' and 'rootdse'. 
#   Defaults to 'rootdse'.
# @param dirsrv_allow_ipv4_address
#   IPv4 address/subnet from which to allow connections to the Directory Server. 
#   Use 'any' for any address. Defaults to '127.0.0.1'.
# @param dirsrv_allow_ipv6_address
#   IPv6 address/subnet from which to allow connections to the Directory Server. 
#   Use 'any' for any address. Defaults to '::1'.
# @param dirsrv_allow_ports
#   Ports to open in the firewall. Defaults to [ 389, 636 ].
# @param self_sign_cert
#   Whether to create a self-signed cert for the directory server
# @param self_sign_cert_valid_months
#   Validity of the self-signed cert (if any)
# @param create_suffix_entry
#   Whether to create a generic root node entry fot the default suffix in the
#   database.
# @param sample_entries
#   Whether to add sample entries to the database. Mainly for use with Vagrant.
# @param monitor_email
#   Where (backup) cronjobs should send email
# @param backups
#   A hash of dirsrv::backup resources
#
class dirsrv (
  String                  $suffix,
  String                  $rootdn_pwd,
  String                  $config_directory_admin_pwd,
  String                  $full_machine_name = $facts['networking']['fqdn'],
  Boolean                 $manage = true,
  Boolean                 $manage_config = false,
  Boolean                 $manage_packetfilter = false,
  Boolean                 $manage_epel = true,
  String                  $serveridentifier = $facts['networking']['hostname'],
  Boolean                 $self_sign_cert = false,
  Integer                 $self_sign_cert_valid_months = 24,
  Boolean                 $create_suffix_entry = false,
  Boolean                 $sample_entries = false,
  Integer[1,65535]        $ldap_port = 389,
  String                  $rootdn = 'cn=Directory Manager',
  String                  $config_directory_ldap_url = 'ldap://localhost:389/o=NetscapeRoot',
  String                  $config_directory_admin_id = 'admin',
  String                  $allow_anonymous_access = 'rootdse',
  Stdlib::IP::Address::V4  $dirsrv_allow_ipv4_address = '127.0.0.1',
  Stdlib::IP::Address::V6  $dirsrv_allow_ipv6_address = '::1',
  Array[Integer[1,65535]] $dirsrv_allow_ports = [389, 636],
  Hash                    $backups = {},
  Optional[String]        $monitor_email = undef,

) inherits dirsrv::params {
  if $manage {
    class { 'dirsrv::install':
      manage_epel => $manage_epel,
    }

    if $manage_config {
      class { 'dirsrv::config':
        full_machine_name           => $full_machine_name,
        serveridentifier            => $serveridentifier,
        ldap_port                   => $ldap_port,
        suffix                      => $suffix,
        rootdn                      => $rootdn,
        rootdn_pwd                  => $rootdn_pwd,
        config_directory_ldap_url   => $config_directory_ldap_url,
        config_directory_admin_id   => $config_directory_admin_id,
        config_directory_admin_pwd  => $config_directory_admin_pwd,
        allow_anonymous_access      => $allow_anonymous_access,
        self_sign_cert              => $self_sign_cert,
        self_sign_cert_valid_months => $self_sign_cert_valid_months,
        create_suffix_entry         => $create_suffix_entry,
        sample_entries              => $sample_entries,
      }
    }

    class { 'dirsrv::service':
      serveridentifier => $serveridentifier,
    }

    if $manage_packetfilter {
      class { 'dirsrv::packetfilter::dirsrv':
        allow_ipv4_address => $dirsrv_allow_ipv4_address,
        allow_ipv6_address => $dirsrv_allow_ipv6_address,
        allow_ports        => $dirsrv_allow_ports,
      }
    }

    # Create dirsrv::backup resources
    create_resources('dirsrv::backup', $backups)
  }
}
